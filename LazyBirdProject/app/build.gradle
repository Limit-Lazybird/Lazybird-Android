plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin' // dagger hilt
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin" // navigation safeargs
    alias libs.plugins.secrets.gradle
}

android {
    defaultConfig {
        applicationId "com.limit.lazybird"
        minSdkVersion 29
        targetSdkVersion 33
        compileSdk 33
        versionCode 12
        versionName "1.0.11"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def keyProperties = new Properties()
        keyProperties.load(project.rootProject.file("key.properties").newDataInputStream())
        buildConfigField "String", "GOOGLE_SERVER_CLIENT_ID", "\"${keyProperties.getProperty("GOOGLE_SERVER_CLIENT_ID")}\""
        buildConfigField "String", "KAKAO_NATIVE_APP_KEY", "\"${keyProperties.getProperty("KAKAO_NATIVE_APP_KEY")}\""
        buildConfigField "String", "KAKAO_OAUTH_SCHEME", "\"${keyProperties.getProperty("KAKAO_OAUTH_SCHEME")}\""
        buildConfigField "String", "LAZYBIRD_SERVER_URL", "\"${keyProperties.getProperty("LAZYBIRD_SERVER_URL")}\""
        manifestPlaceholders = [KAKAO_OAUTH_SCHEME:"\"${keyProperties.getProperty("KAKAO_OAUTH_SCHEME")}\""]
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "server-version"
    productFlavors {
        real {
            dimension "server-version"
            applicationIdSuffix ".real"
            versionNameSuffix "-real"
            buildConfigField "boolean", "IS_DEV_SERVER", "false"
        }
        dev {
            dimension "server-version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField "boolean", "IS_DEV_SERVER", "true"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true // for CalendarView
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    namespace 'com.limit.lazybird'
}

dependencies {

    implementation libs.jetbrains.kotlin
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.legacy.support
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    // CalendarView
    implementation libs.calendarview
    coreLibraryDesugaring libs.desugar

    // Dagger hilt
    implementation libs.hilt.android
    kapt libs.hilt.compiler

    // DataStore
    implementation libs.androidx.datastore

    // Flexbox
    implementation libs.flexbox

    // Fragment for fragmentResult
    implementation libs.androidx.fragment

    // Glide
    implementation libs.glide
    annotationProcessor libs.glide.compiler

    // Google login
    implementation libs.google.playstore

    // Kakao login
    implementation libs.kakao.sdk

    // Lifecycle
    implementation libs.androidx.lifecycle.viewModel
    implementation libs.androidx.lifecycle.liveData

    // Navigation fragment
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui

    // Retrofit
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson

    // ViewPager
    implementation libs.androidx.cardview
    implementation libs.androidx.viewpager2

}
